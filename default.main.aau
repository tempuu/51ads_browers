import win.ui;
import win.ui.menu
/*DSG{{*/
var winform = win.form(text="AAuto Explorer(可编程浏览器)";right=610;bottom=563;maxmize=1;parent=...)
winform.add(
bthome={cls="button";text="主页";left=557;top=144;right=582;bottom=205;dr=1;dt=1;z=5};
btnback={cls="button";text="后退";left=557;top=290;right=582;bottom=351;dr=1;dt=1;z=7};
btnClose={cls="button";text="关闭";left=557;top=71;right=582;bottom=132;dr=1;dt=1;z=10};
btnforward={cls="button";text="前进";left=557;top=214;right=582;bottom=275;dr=1;dt=1;z=6};
btnGo={cls="button";text="Go";left=448;top=5;right=531;bottom=31;dr=1;dt=1;z=4};
btnrefresh={cls="button";text="刷新";left=557;top=359;right=582;bottom=420;dr=1;dt=1;z=8};
btnrefresh3={cls="button";text="硬刷新";left=557;top=426;right=582;bottom=487;dr=1;dt=1;z=9};
button={cls="button";text="button";left=4;top=525;right=125;bottom=559;z=11};
editUrl={cls="edit";left=52;top=4;right=432;bottom=27;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(name='宋体');tabstop=1;z=3};
static={cls="static";text="地址：";left=12;top=11;right=52;bottom=27;dl=1;dt=1;font=LOGFONT(name='宋体');transparent=1;z=2};
tab={cls="tab";left=8;top=31;right=552;bottom=481;cp=1;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(name='宋体');z=1}
)
/*}}*/

//开启自动完成 
import win.util.autoComplete;
win.util.autoComplete(winform.editUrl,6/*_SHACF_URLALL*/)

winform.editUrl.wndproc = function(hwnd,message,wParam,lParam){ 
    if (  message == 0x101/*_WM_KEYUP*/ ) { 
        if ( wParam == 0xD/*_VK_RETURN*/ ) {
            addWebPage( winform.editUrl.text );
        }
    } 
}//endproc

 import web.form; 

//创建状态栏
import win.ui.statusbar;
var stat = win.ui.statusbar( winform );

//当网址发生改变
onUrlChange = function () { 
    if(winform.tab.form ){
        winform.editUrl.text = winform.tab.form.web.location; 
        winform.btnback.disabled = winform.tab.form.web.btnback
        winform.btnforward.disabled = winform.tab.form.web.btnforward
    }
    else {
        winform.editUrl.text = "";
    }
}

import web.form.util;
//启用网页对选项卡的支持，例如鼠标中键打开等等 
web.form.util.enableTab();

//创建全局数组
..webarr={};

 //import string.mbcs //中文增强函数库
addWebPage = function ( url ) {
	
    //创建web窗体
    var form = winform.tab.add( text = "Loading...";scroll=true ); 
    var wb = web.form( form , , 0x40000000/*_DLCTL_SILENT*/ | 0x10/*_DLCTL_DLIMAGES*/| 0x20/*_DLCTL_VIDEOS*/  );

    //获得索引位置
   	 var sel = #winform.tab.items ;
    //winform.tab.selIndex = sel;
    

    //web窗体事件 
    var preTitle;
    wb.TitleChange = function(title){ 
        if(preTitle==title)
            return; 

        preTitle = title
        var str = string.mbcs( title )  
        winform.tab.setItemText(sel,str.limit(15/*_MAX_WEBTITLE*/)  ) 
    } 
    //新窗口事件
    wb.NewWindow3=function(ppDisp, Cancel,dwFlags,bstrUrlContext, bstrUrl ) {
        wb.NewWindow2 = null;
        /*弹出新窗口以前触发 */
        if(dwFlags & 0x10000/*_NWMF_FORCEWINDOW*/ )
            return;//如果用户选择在新窗口打开，不处理

        //否则,在新的选项卡打开
        return addWebPage( "" ).application;
    } 
    wb.NewWindow2 = function ( ppDisp, Cancel ) {  
        return addWebPage( "" ).application;
    }
    wb.NavigateComplete2 = function (pDisp, url) {
        onUrlChange();  
        //wb.refresh()
    } 
    wb.CommandStateChange=function(cmd,enable) {
        /*当命令的激活状态改变时触发。它表明何时激活或关闭Back和Forward菜单项或按钮 */
        select(cmd) {

            case 0x2/*_CSC_NAVIGATEBACK*/ {
                winform.btnback.disabled = !enable
                wb.btnback = !enable 

            }
            case 0x1/*_CSC_NAVIGATEFORWARD*/ {
                winform.btnforward.disabled = !enable
                wb.btnforward = !enable
            }  
        }
     } 
    wb.StatusTextChange = function ( text ) {
        if ( winform.tab.form == wb._form )
            stat.setText( text );
    }
    wb.ProgressChange = function ( progress, progressMax ) {
        if ( progressMax && winform.tab.form == wb._form )
            stat.setText( "正在载入..." + (progress / progressMax * 100) + "%" );
    } 

    //打开网址
    wb.go( url );   
	..webarr[url]=wb;
    return wb;
}


addWebPage("http://www.5iads.cn/zhuan.asp?zhuan=dianji");

//开始任务浏览模式
winform.button.oncommand = function(id,event){
	io.open();
	//winform.msgbox( winform.button.text );
	strurl="http://www.5iads.cn/zhuan.asp?zhuan=dianji";
	temp_wb=..webarr[strurl];
	temp_wb.wait("http://www.5iads.cn/zhuan.asp?zhuan=dianji",5000);
	js = /*

		window.alert = function(str){ return ;}

	*/
	temp_wb.doScript(js);
	
	var ele = temp_wb.queryEles( className="zhuanclick"  )
	num=#ele;
	
	
	bro_ad=function(num){
		io.print(num);
		if( #winform.tab.items==2 ){
			winform.tab.remove(2)
		}
		if(num==0){
			temp_wb.refresh();
			
			var ele = temp_wb.queryEles( className="zhuanclick"  )
			temp_wb.waitEle("txt_pagesum")			
			num=#ele+1;
		}
		
		num=num-1;
		
		var ele = temp_wb.queryEles( className="zhuanclick"  );
		ele(0).click();
		
		winform.setTimeout(bro_ad,20000,num);
	}
	
	bro_ad(num);
	//winform.tab.remove(2);
}


/*控制网页按钮部份{{*/
    winform.btnGo.oncommand = function(id,event){ 
        addWebPage( winform.editUrl.text ); 
    }//endproc
    winform.btnClose.oncommand = function(id,event){
         if(!winform.tab.selIndex)
            return;

         winform.tab.remove(winform.tab.selIndex)  
         onUrlChange();
    }//endproc
    winform.btnrefresh3.oncommand = function(id,event){
        if(!winform.tab.selIndex)
            return;

        winform.tab.form.web.refresh3()
    }//endproc
    winform.btnrefresh.oncommand = function(id,event){
        if(!winform.tab.selIndex)
            return;

        winform.tab.form.web.refresh()
    }//endproc
    winform.btnback.oncommand = function(id,event){
        winform.tab.form.web.goback()
    }//endproc
    winform.btnforward.oncommand = function(id,event){
        winform.tab.form.web.goforward();   
    }//endproc
    winform.bthome.oncommand = function(id,event){
        if(!winform.tab.form)
            addWebPage( "about:blank");
        winform.tab.form.web.gohome()
    }//endproc
/*}}*/


/*选项卡的右键的菜单{{*/
    //创建弹出菜单,当右键点击选项卡时,弹出关闭窗口的菜单
    winform.popmenu = win.ui.popmenu(winform);
    winform.popmenu.add(
        '刷新',
        function(id){
            winform.tab.form.web.refresh3()
        }
    );
    winform.popmenu.add(
        '关闭',
        function(id){ 
            if(!winform.tab.selIndex)
                return ; 
            winform.tab.remove( winform.tab.selIndex )  
            onUrlChange();
        }
    ) 
    winform.popmenu.add();
    winform.popmenu.add(
        '全部关闭',
        function(id){ 
            winform.tab.removeAll();
            onUrlChange();
        }
    )
/*}}*/



/*消息处理函数{{*/
    //当用户切换选项卡,触发此函数
    winform.tab.onnotify = function(id,code,ptr){
        if ( code == 0xFFFFFDD9/*_TCN_SELCHANGE*/ ) {
            onUrlChange();
        }
    }


    //tab控件消息回调函数
    winform.tab.wndproc = function(hwnd,message,wParam,lParam){
        if ( message == 0x207/*_WM_MBUTTONDOWN*/ or message = 0x203/*_WM_LBUTTONDBLCLK*/) { 
            //鼠标中键点击选项卡或双击,关闭窗口
            var i = winform.tab.fromPoint( ::LOWORD(lParam), ::HIWORD(lParam) ); 
            if ( i ) {
                winform.tab.remove(i)  
                onUrlChange();
            } 
        }
        else if (message = 0x204/*_WM_RBUTTONDOWN*/){
            //鼠标右键点击选项卡,弹出菜单
            var rc = ::RECT()
            rc.left,rc.top = ::LOWORD(lParam), ::HIWORD(lParam)
            ::ClientToScreen(winform.tab.hwnd,rc);
            winform.popmenu.popup(rc.left,rc.top,true);//弹出菜单
        }

    }
/*}}*/

winform.show() 
win.loopMessage();
